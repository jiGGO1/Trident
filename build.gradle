buildscript {
    repositories {
        maven { url = "https://files.minecraftforge.net/maven" }
        maven { url = "https://repo.spongepowered.org/maven" }
        jcenter()
    }
    dependencies {
        classpath 'com.anatawa12.forge:ForgeGradle:2.3-1.0.+'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'idea'

version = "1.0.5"
group = "net.minecraft.trident"
archivesBaseName = "Trident"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2837"
    runDir = "run"
    mappings = "snapshot_20171003"
    clientJvmArgs += "-Dfml.coreMods.load=${mod_core_plugin}"
    serverJvmArgs += "-Dfml.coreMods.load=${mod_core_plugin}"
    clientRunArgs += "--mixin ${mod_mixin_configs}"
    serverRunArgs += "--mixin ${mod_mixin_configs}"
}

dependencies {
    //deobfCompile "net.ilexiconn:llibrary:1.7.9-1.12.2:dev"
    deobfCompile "curse.maven:llibrary-243298:2505001"
    deobfCompile "curse.maven:iceandfire-264231:2939529"
    deobfCompile "mezz.jei:jei_1.12.2:4.15.+"
    deobfCompile "curse.maven:coreutil-237749:2902920"
    deobfCompile "curse.maven:weather-237746:2596867"
    deobfCompile "curse.maven:MCglTF-591547:4367036"
    deobfCompile "curse.maven:ModularWarfare-350869:4426196"
    deobfCompile "curse.maven:OceanicExpanse-840576:4670168"
    provided('org.spongepowered:mixin:0.8-SNAPSHOT') {
        exclude module: 'guava'
        exclude module: 'commons-io'
        exclude module: 'gson'
    }
}

repositories {
    maven {
        name = "cursemaven"
        url = "https://www.cursemaven.com"
    }
    maven {
        name = "ilexiconn"
        url = "https://maven.mcmoddev.com"
    }
    maven {
        name = "progwml6"
        url = "https://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = "modmaven"
        url = "https://modmaven.dev"
    }
    maven {
        name = "mixin"
        url = "https://repo.spongepowered.org/maven"
    }
}

mixin {
    add sourceSets.main, "mixins.trident.refmap.json"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    from(configurations.provided.collect {
        it.isDirectory() ? it : zipTree(it)
    }) {
        exclude "LICENSE.txt", "META-INF/MANIFSET.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
    }

    manifest {
        attributes "FMLCorePlugin": mod_core_plugin
        attributes "FMLCorePluginContainsFMLMod": "true"
        attributes "ForceLoadAsMod": "true"
        attributes "TweakClass": "org.spongepowered.asm.launch.MixinTweaker"
        attributes "TweakOrder": "0"
        attributes "MixinConfigs": mod_mixin_configs
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}